group 'eu.xenit.gradle'

def baseVersion = '3.3.1'

def branchName = System.env.BRANCH_NAME

def isRelease = branchName != null && branchName.startsWith("release")
if (isRelease)
    version = "$baseVersion"
else
    version = "$baseVersion-SNAPSHOT"

task wrapper(type: Wrapper) {
  gradleVersion = '4.8'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'java-gradle-plugin'

sourceCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

gradlePlugin {
    plugins {
        applyAmps {
            id = "xenit-applyamps"
            implementationClass = "eu.xenit.gradle.applyamps.ApplyAmpsPlugin"
        }
        xenitDocker {
            id = "xenit-docker"
            implementationClass = "eu.xenit.gradle.docker.DockerPlugin"
        }
        xenitDockerbuild {
            id = "xenit-dockerbuild"
            implementationClass = "eu.xenit.gradle.docker.DockerBuildPlugin"
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://artifacts.alfresco.com/nexus/content/groups/public/"
    }
}

dependencies {
    compile gradleApi()
    compile 'org.alfresco:alfresco-mmt:5.2.g'
    compile 'com.bmuschko:gradle-docker-plugin:3.2.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile 'com.avast.gradle:gradle-docker-compose-plugin:0.7.1'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.5'
    compile 'org.eclipse.jgit:org.eclipse.jgit:4.6.0.201612231935-r'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile gradleTestKit()
    testCompile "org.mockito:mockito-core:2.+"
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

apply plugin: 'maven-publish'
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    if(project.hasProperty('eu.xenit.artifactory.username')) {
        repositories {
            maven {
                name 'snapshot'
                url 'https://artifactory.xenit.eu/artifactory/libs-snapshot-local'
                credentials {
                    username property("eu.xenit.artifactory.username")
                    password property("eu.xenit.artifactory.password")
                }
            }
            maven {
                name 'release'
                url 'https://artifactory.xenit.eu/artifactory/libs-release-local'
                credentials {
                    username property("eu.xenit.artifactory.username")
                    password property("eu.xenit.artifactory.password")
                }
            }

        }
    }
}

task integrationTest(type: Test, group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn(integrationTest)
